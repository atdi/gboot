plugins {
    id 'java'
    id 'jacoco'
    id 'pmd'
    id 'findbugs'
    id 'idea'
    id "com.jfrog.bintray" version "1.1"
    id "maven"
}

ext.publish = true

bintray {
    user = project.ext.has('bintrayUser') ? project.ext.bintrayUser : ''
    key = project.ext.has('bintrayApiKey') ? project.ext.bintrayApiKey : ''
    configurations = ['published', 'archives']
    publish = project.publish
    pkg {
        repo = 'maven'
        name = 'gboot-loader'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/atdi/gboot'
        issueTrackerUrl = 'https://github.com/atdi/gboot/issues'
        vcsUrl = 'https://github.com/atdi/gboot.git'
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
        version {
            name = project.version
            vcsTag = "RELEASE-$version"
        }
    }
}

configurations {
    provided
    runtime
    published
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Add the sourceJars to non-extractor modules
artifacts {
    published sourceJar
    published javadocJar
}

compileJava.classpath += project.configurations.provided
compileTestJava.classpath += project.configurations.provided

idea {
    module {
        scopes.PROVIDED.plus += [ configurations.provided ]
        scopes.RUNTIME.plus += [ configurations.runtime ]
    }

}

findbugs {
    ignoreFailures = true
}

findbugsTest.enabled = false
pmdTest.enabled = false

repositories {
    mavenCentral()
}

dependencies {
    provided group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile ('org.mockito:mockito-all:1.10.19')
}

tasks.bintrayUpload.dependsOn "publishPluginPublicationToMavenLocal"